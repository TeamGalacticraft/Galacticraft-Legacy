jar {
    manifest.attributes(getManifestAttributes("Galacticraft"))
}

task coreJar(type: Jar) {
    baseName = "Galacticraft-Core-${mc_version}"
    dependsOn 'reobfJar'
    
    from('etc/core') {
        include '*.info'
        expand 'version':project.version,
               'mc_version':mc_version,
               'forge_version':forge_version
    }
    
    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        exclude 'micdoodle8/mods/galacticraft/planets/**',
                'micdoodle8/mods/miccore/**',
                'assets/galacticraftplanets/**',
                'META-INF/*.cfg', 'META-INF/*.deobf'
    }
    manifest.attributes(getManifestAttributes("GalacticraftCore"))
}

task miccoreJar(type: Jar) {
    baseName = "MicdoodleCore-${mc_version}"
    dependsOn 'reobfJar'

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'micdoodle8/mods/miccore/**',
                'META-INF/*.cfg', 'META-INF/*.deobf',
                'micdoodle8/mods/galacticraft/api/entity/IEntityBreathable.class'
    }
    manifest.attributes(getManifestAttributes("MicdoodleCore"))
    manifest {
        attributes.put("FMLCorePlugin", "micdoodle8.mods.miccore.MicdoodlePlugin")
        attributes.put("FMLAT", "accesstransformer.cfg")
    }
}

task planetsJar(type: Jar) {
    baseName = "Galacticraft-Planets-${mc_version}"
    dependsOn 'reobfJar'
    from('etc/planets') {
        include '*.info'
        expand 'version':project.version,
               'mc_version':mc_version,
               'forge_version':forge_version
    }
    
    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'micdoodle8/mods/galacticraft/planets/**',
                'assets/galacticraftplanets/**',
                'assets/logo.png'
    }
    manifest.attributes(getManifestAttributes("GalacticraftPlanets"))
}

artifactsList += [coreJar, planetsJar, miccoreJar]
